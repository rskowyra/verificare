/* -*-promela-*- 
*for emacs promela mode
* Created < 17-Feb-2007 
* 
* (C) David Farago <farago@kit.edu>
* documentation: http://lfm.iti.kit.edu/files/theses/diploma/daDavidFarago.pdf
*/ 

#define N 3	           /* number of nodes $n$ */
#define K 2               /* rounds mod K=2       */
#define M 3             /* number of IDs = 0,..,M-1  */
#define L 3	          /* size of FIFO-Buffers    */

byte nr_leaders=0;
typedef Msg {byte round; byte id; byte hop; bool unique};
chan q[N] = [L] of {Msg};

proctype node (chan in, out)
{
  bool nodeActive = true; 
  bool leader = false, allSameID;
  byte round, id;
  Msg curMsg; 
  
  xr in;
  xs out;

START:  /* random ID */
  id=0;
  do
    :: skip -> break
    :: id < M-1 -> id++
  od;

  allSameID=true;
  curMsg.id=id;
  curMsg.round=round;
  curMsg.hop=1;
  curMsg.unique=true;

SEND_AND_RECEIVE:
  out!curMsg;

RECEIVE:
  in?curMsg;

/* check message */
  if
    :: curMsg.hop<N ->
       if
	 :: (curMsg.round != round || curMsg.id > id) ->
              allSameID=false; round=curMsg.round; id=curMsg.id; 
              nodeActive=false; curMsg.hop++; 
              goto SEND_AND_RECEIVE
         :: curMsg.round == round && curMsg.id < id ->
	      allSameID=false; goto RECEIVE
         :: curMsg.round==round && curMsg.id==id ->
    	      allSameID=allSameID && curMsg.id==id;
	      curMsg.hop++; curMsg.unique=false; 
              goto SEND_AND_RECEIVE
         :: else -> 
              printf("ERROR: hop<N and msg-cases not correct") 
       fi
    :: curMsg.hop==N ->
       if
	 ::allSameID -> progress1: skip
         ::!allSameID -> skip
       fi;
       if 
         :: nodeActive && !curMsg.unique && round<K-1 -> 
              round++; goto START
         :: nodeActive && !curMsg.unique && round==K-1 -> 
              round=0; goto START
         :: nodeActive && curMsg.unique -> leader=true; 
              nr_leaders++; assert(nr_leaders==1)
         :: else -> printf("ERROR: hop=N and !nodeActive")
       fi
  fi
}

init {
  byte lv;
  atomic {
    lv=0;
    do
      :: lv < N -> run node(q[lv], q[(lv+1)%N]); lv++
      :: lv==N -> break
    od
  }
  timeout -> assert(nr_leaders==1); 
  printf("Asserted: nr_leaders==1")
}
